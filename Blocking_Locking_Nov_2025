sp_who2 active
GO 

WITH BlockingCTE AS
(
    SELECT
        r.session_id AS BlockedSessionID,
        r.blocking_session_id AS BlockingSessionID,
        r.wait_type,
        r.wait_time,
        r.wait_resource,
        r.command AS BlockedCommand,
        DB_NAME(r.database_id) AS BlockedDatabase,
        DB_NAME(b.database_id) AS BlockingDatabase,
        bs.text AS BlockingSQL,
        rs.text AS BlockedSQL,
        s_program.program_name AS BlockingProgram,
        s_blocked.program_name AS BlockedProgram,
        r.start_time AS BlockedStartTime,
        b.start_time AS BlockingStartTime
    FROM sys.dm_exec_requests r
    LEFT JOIN sys.dm_exec_sessions s_blocked
        ON r.session_id = s_blocked.session_id
    LEFT JOIN sys.dm_exec_requests b
        ON r.blocking_session_id = b.session_id
    LEFT JOIN sys.dm_exec_sessions s_program
        ON r.blocking_session_id = s_program.session_id
    OUTER APPLY sys.dm_exec_sql_text(b.sql_handle) bs
    CROSS APPLY sys.dm_exec_sql_text(r.sql_handle) rs
    WHERE r.blocking_session_id <> 0
),

LockInfo AS
(
    SELECT
        tl.request_session_id AS SessionID,
        tl.resource_type,
        tl.resource_description,
        tl.request_mode,
        tl.request_status,
        tl.resource_associated_entity_id AS EntityID,
        DB_NAME(tl.resource_database_id) AS DatabaseName,
        o.name AS ObjectName,
        i.name AS IndexName
    FROM sys.dm_tran_locks tl
    LEFT JOIN sys.objects o
        ON tl.resource_associated_entity_id = o.object_id
    LEFT JOIN sys.indexes i
        ON tl.resource_associated_entity_id = i.object_id
       AND tl.resource_associated_entity_id = i.index_id
    WHERE tl.resource_database_id = DB_ID()
)

SELECT
    B.*,
    L.resource_type AS LockResourceType,
    L.resource_description AS LockResourceDescription,
    L.request_mode AS LockMode,
    L.request_status AS LockStatus,
    L.ObjectName,
    L.IndexName
FROM BlockingCTE B
LEFT JOIN LockInfo L
    ON L.SessionID = B.BlockingSessionID  -- Locks held by the blocker
ORDER BY B.wait_time DESC;

